WHITESPACE = _{ " " }
factor_sign = { "+" | "-" }
factor_value = { ASCII_DIGIT+ }
var_name = @{ (ASCII_ALPHANUMERIC ~ (ASCII_ALPHANUMERIC | "_" | "/" | "+")*) | ("\"" ~ (ASCII_ALPHANUMERIC | " " | "-" | "_" | "/" | "+" | "," | ":" | "$")* ~ "\"") }
first_literal = {factor_sign? ~ (factor_value ~ "*"?)? ~ var_name }
implicit_one_literal = {factor_sign ~ var_name }
literal = {factor_sign ~ factor_value ~ "*"? ~ var_name }
equation_kind = { "=" | "<=" | ">=" | "<" | ">" | "!=" }
equation_side = { first_literal ~ (literal | implicit_one_literal)* }
right_hand_side = { factor_sign? ~ factor_value }
equation = {equation_side ~ equation_kind ~ right_hand_side ~ ";"?}
header = {"#variable=" ~ number_variables ~ "#constraint=" ~ number_constraints ~ NEWLINE}
number_variables = { ASCII_DIGIT+ }
number_constraints = { ASCII_DIGIT+ }
opb_file = { SOI ~ (NEWLINE | ("*" ~ (!NEWLINE ~ ANY)* ~ NEWLINE))* ~ header ~ equation ~ (NEWLINE+ ~ equation)* ~ NEWLINE* ~ EOI }